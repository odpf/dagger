buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

}

plugins {
    id 'maven-publish'
}

ext {
    flinkVersion = System.getenv('flinkVersion') ?: '1.14.3'
}

def pyZipVersion = '0.0.1'
group 'io.odpf'

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://repository.apache.org/content/repositories/snapshots/" }
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion '7.6.1'
        configFile rootProject.file("config/checkstyle/checkstyle.xml")
    }
}

task makePyZip(type: Zip) {
    from fileTree(dir: 'dagger-py-functions')
    include '**/*'
    archiveName "dagger-py-functions.zip"
    destinationDir file("$rootDir/")
}

publishing {
    publications {
        pyZip(MavenPublication) {
            artifact source: makePyZip, extension: 'zip'
            groupId group
            artifactId 'dagger-py-functions'
            version pyZipVersion
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/odpf/dagger"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}